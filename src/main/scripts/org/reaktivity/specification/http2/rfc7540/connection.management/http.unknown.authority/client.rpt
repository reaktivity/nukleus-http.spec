#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newServerAcceptRef ${nukleus:newRouteRef()} # external

connect await ROUTED_SERVER
        "nukleus://http2/streams/source"
        option nukleus:route ${newServerAcceptRef}
        option nukleus:window 8192
        option nukleus:transmission "duplex"
connected

# client connection preface
write "PRI * HTTP/2.0\r\n"
      "\r\n"
      "SM\r\n"
      "\r\n"
write flush

# server connection preface - SETTINGS frame
read [0x00 0x00 0x0c]                   # length = 12
     [0x04]                             # HTTP2 SETTINGS frame
     [0x00]                             # flags = 0x00
     [0x00 0x00 0x00 0x00]              # stream_id = 0
     [0x00 0x03 0x00 0x00 0x00 0x64]    # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
     [0x00 0x04 0x00 0x00 0x00 0x00]    # SETTINGS_INITIAL_WINDOW_SIZE(0x03) = 0

write [0x00 0x00 0x06]                   # length = 6
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x04 0x00 0x00 0xff 0xff]    # SETTINGS_INITIAL_WINDOW_SIZE(0x04) = 65535
write flush

read [0x00 0x00 0x00]                   # length = 0
     [0x04]                             # HTTP2 SETTINGS frame
     [0x01]                             # ACK
     [0x00 0x00 0x00 0x00]              # stream_id = 0

write [0x00 0x00 0x00]                  # length = 0
      [0x04]                            # HTTP2 SETTINGS frame
      [0x01]                            # ACK
      [0x00 0x00 0x00 0x00]             # stream_id = 0
write flush

write [0x00 0x00 0x13]                  # length = 19
      [0x01]                            # HEADERS frame
      [0x05]                            # END_HEADERS | END_STREAM
      [0x00 0x00 0x00 0x01]             # stream_id = 1
      [0x82]                            # :method: GET
      [0x86]                            # :scheme: http
      [0x84]                            # :path: /
      [0x01] [0x0e] "localhost:8081"    # :authority: localhost:8081
write flush

read [0x00 0x00 0x01]                                      # length
     [0x01]                                                # HTTP2 HEADERS frame
     [0x05]                                                # END_HEADERS | END_STREAM
     [0x00 0x00 0x00 0x01]                                 # stream_id=1
     [0x8d]                                                # :status: 404
