#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newTargetInputRef ${nuklei:newReferenceId()} # external scope

property nuklei ${nuklei:directory("target/nukleus-itests").streamsCapacity(1024 * 1024, 64 * 1024)}

property sourceInput ${nuklei.streams("http2", "source")}
property sourceHandshakeEst ${nuklei.streams("source", "http2#source")}
property targetInput ${nuklei.streams("target", "http2#source")}
property targetOutputEst ${nuklei.streams("http2", "target")}
property sourceOutputEst ${nuklei.streams("source", "http2#target")}

property newTargetInputId ${nuklei:newStreamId()}
property newSourceOutputEstId ${nuklei:newStreamId()}
property newTargetCorrelationId ${nuklei:newCorrelationId()}

#
#
# reading from source-->nukleus (http2/source)
#
#
connect await ROUTED_INPUT
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceInput.buffer)}
        option writer ${agrona:oneToOneWriter(sourceInput.throttle)}
connected

# begin frame
read [0x00 0x00 0x00 0x01]
read ([0..8]:sourceInputId)
read ([0..8]:sourceInputRef)
read ([0..8]:sourceCorrelationId)

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceInputId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (http2 client connection preface)
read [0x00 0x00 0x00 0x02]
read ${sourceInputId}
read [0x00 0x18]
read "PRI * HTTP/2.0\r\n"
      "\r\n"
      "SM\r\n"
      "\r\n"

read notify CONNECTION_ESTABLISHED

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceInputId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (http2 SETTINGS frame)
read [0x00 0x00 0x00 0x02]
read ${sourceInputId}
read [0x00 0x0f]
read [0x00 0x00 0x06]                   # length = 6
     [0x04]                             # HTTP2 SETTINGS frame
     [0x00]                             # flags = 0x00
     [0x00 0x00 0x00 0x00]              # stream_id = 0
     [0x00 0x04 0x00 0x00 0xff 0xff]    # SETTINGS_INITIAL_WINDOW_SIZE(0x04) = 65535

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceInputId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (http2 SETTINGS frame)
read [0x00 0x00 0x00 0x02]
read ${sourceInputId}
read [0x00 0x09]
read [0x00 0x00 0x00]           # length = 0
     [0x04]                     # HTTP2 SETTINGS frame
     [0x01]                     # ACK
     [0x00 0x00 0x00 0x00]      # stream_id = 0

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceInputId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (http2 HEADERS frame)
read [0x00 0x00 0x00 0x02]
read ${sourceInputId}
read [0x00 0x18]
read [0x00 0x00 0x0f]                                                   # length = 15
     [0x01]                                                             # HEADERS frame
     [0x05]                                                             # END_STREAM | END_HEADERS
     [0x00 0x00 0x00 0x01]                                              # stream_id = 1
     [0x82]                                                             # :method: GET
     [0x86]                                                             # :scheme: http
     [0x84]                                                             # :path: /
     [0x41] [0x8a 0xa0 0xe4 0x1d 0x13 0x9d 0x09 0xb8 0xf0 0x1e 0x07]    # :authority: localhost:8080


read notify HTTP_REQUEST_HEADERS_DECODED

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceInputId}
write [0x00 0x02 0x00 0x00]
write flush


#
#
# writing to source-->nukleus-->source (source/http2#source)
#
#
connect await CONNECTION_ESTABLISHED
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceHandshakeEst.throttle)}
        option writer ${agrona:oneToOneWriter(sourceHandshakeEst.buffer)}
connected

# begin frame
write [0x00 0x00 0x00 0x01]
write ${newSourceOutputEstId}
write [0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00]
write ${sourceCorrelationId}
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceOutputEstId}
read [0..4]

# data frame (server connection preface - SETTINGS frame)
write [0x00 0x00 0x00 0x02]
write ${newSourceOutputEstId}
write [0x00 0x0F]
write [0x00 0x00 0x06]                   # length = 6
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x03] [0x00 0x00 0x00 0x64]  # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceOutputEstId}
read [0..4]

# data frame (http2 SETTINGS frame)
write [0x00 0x00 0x00 0x02]
write ${newSourceOutputEstId}
write [0x00 0x09]
write [0x00 0x00 0x00]           # length = 0
      [0x04]                     # HTTP2 SETTINGS frame
      [0x01]                     # ACK
      [0x00 0x00 0x00 0x00]      # stream_id = 0
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceOutputEstId}
read [0x17 0x00 0x00 0x00]


#
#
# writing to nukleus-->target (target/http2#source)
#
#
connect await HTTP_REQUEST_HEADERS_DECODED
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(targetInput.throttle)}
        option writer ${agrona:oneToOneWriter(targetInput.buffer)}
connected

write [0x00 0x00 0x00 0x01]
write ${newTargetInputId}
write ${newTargetInputRef}
write ${newTargetCorrelationId}
write [0x00] [0x07] ":method" [0x03] "GET"
write [0x00] [0x07] ":scheme" [0x04] "http"
write [0x00] [0x05] ":path" [0x01] "/"
write [0x00] [0x0a] ":authority" [0x0e] "localhost:8080"
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newTargetInputId}
read [0..4]


#
#
# reading from target-->nukleus (http2/target)
#
#
connect await ROUTED_OUTPUT
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(targetOutputEst.buffer)}
        option writer ${agrona:oneToOneWriter(targetOutputEst.throttle)}
connected

# begin frame (with http headers)
read [0x00 0x00 0x00 0x01]
read ${newTargetOutputEstId}
read [0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00]
read ${targetCorrelationId}
read [0x00] [0x07] ":status" [0x03] "404"
read [0x00] [0x06] "server" [0x14] "CERN/3.0 libwww/2.17"
read [0x00] [0x04] "date" [0x1d] "Wed, 01 Feb 2017 19:12:46 GMT"
read [0x00] [0x0c] "content-type" [0x18] "text/html; charset=UTF-8"
read [0x00] [0x0e] "content-length" [0x03] "147"

read notify HTTP_RESPONSE_HEADERS_RECEIVED

# window frame
write [0x40 0x00 0x00 0x02]
write ${newTargetOutputEstId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (with http payload)
read [0x00 0x00 0x00 0x02]
read ${newTargetOutputEstId}
read [0x00 0x3d]
read "<html><head></head><body><h1>404 Not Found</h1></body></html>"

read notify HTTP_RESPONSE_PAYLOAD_RECEIVED

# window frame
write [0x40 0x00 0x00 0x02]
write ${newTargetOutputEstId}
write [0x00 0x02 0x00 0x00]
write flush

# end frame
read [0x00 0x00 0x00 0x03]
read ${newTargetOutputEstId}

read notify HTTP_RESPONSE_PAYLOAD_DONE



#
#
# writing to target-->nukleus-->source (source/http2#target)
#
#

connect await HTTP_RESPONSE_HEADERS_RECEIVED
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceOutputEst.throttle)}
        option writer ${agrona:oneToOneWriter(sourceOutputEst.buffer)}
connected

# begin frame is already written (same tcp stream)
# nuklei data frame (HTTP2 HEADERS)
write [0x00 0x00 0x00 0x02]
write ${newSourceOutputEstId}
write [0x00 0x62]
write [0x00 0x00 0x59]                                      # length
      [0x01]                                                # HTTP2 HEADERS frame
      [0x04]                                                # END_HEADERS
      [0x00 0x00 0x00 0x01]                                 # stream_id=1
      [0x8d]                                                # :status: 404
      [0x0f 0x27] [0x14] "CERN/3.0 libwww/2.17"             # server
      [0x0f 0x12] [0x1d] "Wed, 01 Feb 2017 19:12:46 GMT"    # date
      [0x0f 0x10] [0x18] "text/html; charset=UTF-8"         # content-type
      [0x0f 0x0d] [0x03] "147"                              # content-length

write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceOutputEstId}
read [0x00 0x02 0x00 0x00]

write await HTTP_RESPONSE_PAYLOAD_RECEIVED

# nuklei data frame (HTTP2 DATA)
write [0x00 0x00 0x00 0x02]
write ${newSourceOutputEstId}
write [0x00 0x46]
write [0x00 0x00 0x3d]                          # length = 61
      [0x00]                                    # HTTP2 DATA frame
      [0x00]                                    # no flags
      [0x00 0x00 0x00 0x01]                     # stream_id=1
write "<html><head></head><body><h1>404 Not Found</h1></body></html>"
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceOutputEstId}
read [0x13 0x00 0x00 0x00]

write await HTTP_RESPONSE_PAYLOAD_DONE

# nuklei data frame (HTTP2 DATA + END_STREAM)
write [0x00 0x00 0x00 0x02]
write ${newSourceOutputEstId}
write [0x00 0x09]
write [0x00 0x00 0x00]                          # length = 0
      [0x00]                                    # HTTP2 DATA frame
      [0x01]                                    # END_STREAM
      [0x00 0x00 0x00 0x01]                     # stream_id=1
write flush






